apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    signingConfigs {
        demokey {
            storeFile file('demokey.jks')
            storePassword "demokey"
            keyAlias 'demokey'
            keyPassword 'demokey'
        }
    }
    defaultConfig {
        applicationId "com.hzy.weex.frame"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1_00_01
        versionName "1.0.1"
        ndk {
            abiFilters 'armeabi-v7a'
        }
        resConfigs "zh-rCN"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.demokey
        }
        release {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.demokey
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName = project.name + '-' + variant.name + '-V' +
                    defaultConfig.versionName + "-" + buildTime() + ".apk"
            outputFileName = fileName
        }
    }
}

static def buildTime() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault())
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.alibaba:fastjson:1.1.70.android'
    implementation 'com.taobao.android:weex_sdk:0.20.0.2'
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.blankj:utilcode:1.23.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0'
    implementation('com.alibaba:arouter-api:1.4.1') {
        exclude group: 'com.android.support'
    }
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
}
