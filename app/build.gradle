apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    signingConfigs {
        demokey {
            storeFile file('demokey.jks')
            storePassword "demokey"
            keyAlias 'demokey'
            keyPassword 'demokey'
        }
    }
    defaultConfig {
        applicationId "com.hzy.weex.frame"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1_00_04
        versionName "1.0.4"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
        resConfigs "zh-rCN"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.demokey
        }
        release {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.demokey
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName = project.name + '-' + variant.name + '-V' +
                    defaultConfig.versionName + "-" + buildTime() + ".apk"
            outputFileName = fileName
        }
    }
}

static def buildTime() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault())
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.alibaba:fastjson:1.1.71.android'
    implementation 'com.taobao.android:weex_sdk:0.26.0'
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.7'
    implementation 'com.squareup.okhttp3:okhttp:4.2.1'
    implementation 'com.blankj:utilcode:1.25.9'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    implementation 'com.alibaba:arouter-api:1.5.0'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
}
